;***********************************************************************
; MICROCOSM ASSOCIATES  8080/8085 CPU DIAGNOSTIC VERSION 1.0  (C) 1980
;***********************************************************************
;
;DONATED TO THE "SIG/M" CP/M USER'S GROUP BY:
;KELLY SMITH, MICROCOSM ASSOCIATES
;3055 WACO AVENUE
;SIMI VALLEY, CALIFORNIA, 93065
;(805) 527-9321 (MODEM, CP/M-NET (TM))
;(805) 527-0518 (VERBAL)
;
@Start	.equ	*
	CPI	$00	;A=F5H,C=0,Z=0
	JC	@CPIE	;TEST "CPI" FOR RE-SET CARRY
	JZ	@CPIE	;TEST "CPI" FOR RE-SET ZERO
	CPI	$F5	;A=F5H,C=0,Z=1
	JC	@CPIE	;TEST "CPI" FOR RE-SET CARRY ("ADI")
	JNZ	@CPIE	;TEST "CPI" FOR RE-SET ZERO
	CPI	$FF	;A=F5H,C=1,Z=0
	JZ	@CPIE	;TEST "CPI" FOR RE-SET ZERO
	JC	@ACII	;TEST "CPI" FOR SET CARRY
@CPIE	CALL	CPUError
@ACII	ACI	$0A	;A=F5H+0AH+CARRY(1)=0,C=1
	ACI	$0A	;A=0+0AH+CARRY(0)=0BH,C=0
	CPI	$0B
	JZ	@SUII	;TEST "ACI"
	CALL	CPUError
@SUII	SUI	$0C	;A=FFH,C=0
	SUI	$0F	;A=F0H,C=1
	CPI	$F0
	JZ	@SBII	;TEST "SUI"
	CALL	CPUError
@SBII	SBI	$F1	;A=F0H-0F1H-CARRY(0)=FFH,C=1
	SBI	$0E	;A=FFH-OEH-CARRY(1)=F0H,C=0
	CPI	$F0
	JZ	@ANII	;TEST "SBI"
	CALL	CPUError
@ANII	ANI	$55	;A=F0H<AND>55H=50H,C=0,P=1,S=0,Z=0
	CPI	$50
	JZ	@ORII	;TEST "ANI"
	CALL	CPUError
@ORII	ORI	$3A	;A=50H<OR>3AH=7AH,C=0,P=0,S=0,Z=0
	CPI	$7A
	JZ	@XRII	;TEST "ORI"
	CALL	CPUError
@XRII:	XRI	$0F	;A=7AH<XOR>0FH=75H,C=0,P=0,S=0,Z=0
	CPI	$75
	JZ	@End	;TEST "XRI"
	CALL	CPUError
@End	.equ	*
