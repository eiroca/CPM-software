;***********************************************************************
; MICROCOSM ASSOCIATES  8080/8085 CPU DIAGNOSTIC VERSION 1.0  (C) 1980
;***********************************************************************
;
;DONATED TO THE "SIG/M" CP/M USER'S GROUP BY:
;KELLY SMITH, MICROCOSM ASSOCIATES
;3055 WACO AVENUE
;SIMI VALLEY, CALIFORNIA, 93065
;(805) 527-9321 (MODEM, CP/M-NET (TM))
;(805) 527-0518 (VERBAL)
;
@Start	.equ	*
	ANI	$00	;A=0,C=0,P=1,S=0,Z=1
	CC	CPUError	;TEST "CC"
	CPO	CPUError	;TEST "CPO"
	CM	CPUError	;TEST "CM"
	CNZ	CPUError	;TEST "CNZ"
	CPI	$00
	JZ	@C020	;A=0,C=0,P=0,S=0,Z=1
	CALL	CPUError
@C020	SUI	$77	;A=89H,C=1,P=0,S=1,Z=0
	CNC	CPUError	;TEST "CNC"
	CPE	CPUError	;TEST "CPE"
	CP	CPUError	;TEST "CP"
	CZ	CPUError	;TEST "CZ"
	CPI	$89
	JZ	@C030	;TEST FOR "CALLS" TAKING BRANCH
	CALL	CPUError
@C030	ANI	$FF	;SET FLAGS BACK!
	CPO	@CPOI	;TEST "CPO"
	CPI	$D9
	JZ	@End	;TEST "CALL" SEQUENCE SUCCESS
	CALL	CPUError
@CPOI	RPE		;TEST "RPE"
	ADI	$10	;A=99H,C=0,P=0,S=1,Z=0
	CPE	@CPEI	;TEST "CPE"
	ADI	$02	;A=D9H,C=0,P=0,S=1,Z=0
	RPO		;TEST "RPO"
	CALL	CPUError
@CPEI	RPO		;TEST "RPO"
	ADI	$20	;A=B9H,C=0,P=0,S=1,Z=0
	CM	@CMI	;TEST "CM"
	ADI	$04	;A=D7H,C=0,P=1,S=1,Z=0
	RPE		;TEST "RPE"
	CALL	CPUError
@CMI	RP		;TEST "RP"
	ADI	$80	;A=39H,C=1,P=1,S=0,Z=0
	CP	@TCPI	;TEST "CP"
	ADI	$80	;A=D3H,C=0,P=0,S=1,Z=0
	RM		;TEST "RM"
	CALL	CPUError
@TCPI	RM		;TEST "RM"
	ADI	$40	;A=79H,C=0,P=0,S=0,Z=0
	CNC	@CNCI	;TEST "CNC"
	ADI	$40	;A=53H,C=0,P=1,S=0,Z=0
	RP		;TEST "RP"
	CALL	CPUError
@CNCI	RC		;TEST "RC"
	ADI	$8F	;A=08H,C=1,P=0,S=0,Z=0
	CC	@CCI	;TEST "CC"
	SUI	$02	;A=13H,C=0,P=0,S=0,Z=0
	RNC		;TEST "RNC"
	CALL	CPUError
@CCI	RNC		;TEST "RNC"
	ADI	$F7	;A=FFH,C=0,P=1,S=1,Z=0
	CNZ	@CNZI	;TEST "CNZ"
	ADI	$FE	;A=15H,C=1,P=0,S=0,Z=0
	RC		;TEST "RC"
	CALL	CPUError
@CNZI	RZ		;TEST "RZ"
	ADI	$01	;A=00H,C=1,P=1,S=0,Z=1
	CZ	@CZI	;TEST "CZ"
	ADI	$D0	;A=17H,C=1,P=1,S=0,Z=0
	RNZ		;TEST "RNZ"
	CALL	CPUError
@CZI	RNZ		;TEST "RNZ"
	ADI	$47	;A=47H,C=0,P=1,S=0,Z=0
	CPI	$47	;A=47H,C=0,P=1,S=0,Z=1
	RZ		;TEST "RZ"
	CALL	CPUError
@End	.equ	*
