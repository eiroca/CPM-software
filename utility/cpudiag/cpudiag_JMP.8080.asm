;***********************************************************************
; MICROCOSM ASSOCIATES  8080/8085 CPU DIAGNOSTIC VERSION 1.0  (C) 1980
;***********************************************************************
;
;DONATED TO THE "SIG/M" CP/M USER'S GROUP BY:
;KELLY SMITH, MICROCOSM ASSOCIATES
;3055 WACO AVENUE
;SIMI VALLEY, CALIFORNIA, 93065
;(805) 527-9321 (MODEM, CP/M-NET (TM))
;(805) 527-0518 (VERBAL)
;
@Start	.equ	*
	ANI	$00	;INITIALIZE A REG. AND CLEAR ALL FLAGS
	JZ	@J010	;TEST "JZ"
	CALL	CPUError
@J010	JNC	@J020	;TEST "JNC"
	CALL	CPUError
@J020	JPE	@J030	;TEST "JPE"
	CALL	CPUError
@J030	JP	@J040	;TEST "JP"
	CALL	CPUError
@J040	JNZ	@J050	;TEST "JNZ"
	JC	@J050	;TEST "JC"
	JPO	@J050	;TEST "JPO"
	JM	@J050	;TEST "JM"
	JMP	@J060	;TEST "JMP"
@J050	CALL	CPUError
@J060	ADI	$06	;A=6,C=0,P=1,S=0,Z=0
	JNZ	@J070	;TEST "JNZ"
	CALL	CPUError
@J070	JC	@J080	;TEST "JC"
	JPO	@J080	;TEST "JPO"
	JP	@J090	;TEST "JP"
@J080	CALL	CPUError
@J090	ADI	$70	;A=76H,C=0,P=0,S=0,Z=0
	JPO	@J100	;TEST "JPO"
	CALL	CPUError
@J100	JM	@J110	;TEST "JM"
	JZ	@J110	;TEST "JZ"
	JNC	@J120	;TEST "JNC"
@J110	CALL	CPUError
@J120	ADI	$81	;A=F7H,C=0,P=0,S=1,Z=0
	JM	@J130	;TEST "JM"
	CALL	CPUError
@J130	JZ	@J140	;TEST "JZ"
	JC	@J140	;TEST "JC"
	JPO	@J150	;TEST "JPO"
@J140	CALL	CPUError
@J150	ADI	$FE	;A=F5H,C=1,P=1,S=1,Z=0
	JC	@J160	;TEST "JC"
	CALL	CPUError
@J160	JZ	@J170	;TEST "JZ"
	JPO	@J170	;TEST "JPO"
	JM	@End	;TEST "JM"
@J170	CALL	CPUError
@End	.equ	*
